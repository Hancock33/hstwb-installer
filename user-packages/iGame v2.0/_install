; iGame v2.0
; ----------
;
; Date: 2021-11-26
; Author: Henrik Noerfjand Stengaard
;
; User package for HstWB Installer to automate installation of iGame v2.0.


; dirs
set igamedir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "iGame"`"
set igameicon "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "iGame.info"`"
set muiprefsdir "SYSTEMDIR:Prefs/Env-Archive/mui"

; set igame installed to 1, if igame is installed
set igameinstalled "0"
IF EXISTS "$igamedir"
  set igameinstalled "1"
EndIf

; create igame directory and icons
MakePath >NIL: "$igamedir"
MakeIcon >NIL: "$INSTALLDIR"

; create mui prefs dir, if it doesn't exist
IF NOT EXISTS "$muiprefsdir"
  MakePath >NIL: "$muiprefsdir"
EndIf

; write install status
echo "iGame..."

; extract igame to temp systemdir
lha -m1 -q x "USERPACKAGEDIR:iGame-v2.0-20201016.lha" "SYSTEMDIR:Temp/_iGame/"

; install igame files
Copy >NIL: "SYSTEMDIR:Temp/_iGame/iGame-v2.0.info" "$igameicon" CLONE
Copy >NIL: "SYSTEMDIR:Temp/_iGame/iGame-v2.0/#?" "$igamedir" CLONE ALL
Delete >NIL: "$igamedir/Install-iGame#?"

; install igame mui prefs, if it doesn't exist
IF "$igameinstalled" EQ 0 VAL
  Copy >NIL: "USERPACKAGEDIR:IGAME.1#?" "$muiprefsdir" ALL CLONE
ENDIF

; delete igame from temp systemdir
Delete >NIL: "SYSTEMDIR:Temp/_iGame" ALL

; get device and path from igamedir
echo "$igamedir/iGame" >T:_igamedir1

cut -d ":" -f 1 T:_igamedir1 >T:_igamedir2
sed "1d" T:_igamedir2 >T:_igamedir3
rep T:_igamedir3 " " ""
set device "`type T:_igamedir3`"

cut -d ":" -f 2 T:_igamedir1 >T:_igamedir2
sed "1d" T:_igamedir2 >T:_igamedir3
rep T:_igamedir3 " " ""
set path "`type T:_igamedir3`"

; add igame to backdrop
IF EXISTS "$device:.backdrop"
  search "$device:.backdrop" "$path" >NIL:
  IF NOT $RC EQ 0 VAL
    echo ":$path" >>"$device:.backdrop"
  ENDIF
ELSE
  echo ":$path" >>"$device:.backdrop"
ENDIF

; end
; ---
LAB end
