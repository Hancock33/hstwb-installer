; KingCON v1.3
; ------------
;
; Date: 2021-12-03
; Author: Henrik Noerfjand Stengaard
;
; User package for HstWB Installer to automate installation of KingCON.


; paths
set sdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S"`"
set usfile "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S/User-Startup"`"
set ldir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "L"`"
set devsdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "Devs"`"

; show kingcon cpu version request choice
set cpu `RequestChoice "KingCON CPU version" "Select KingCON CPU version to install*N*NDefault is 68000." "68020|68000|Default"` 

; create user startup file, if it doesn't exist 
IF NOT EXISTS "$usfile"
  Echo "" NOLINE >"$usfile"
ENDIF

; write install status
echo "KingCON..."

; extract kingcon to temp systemdir
lha -m1 -q x "USERPACKAGEDIR:KingCON_1.3.lha" "SYSTEMDIR:Temp/_KingCON/"

If "$cpu" EQ 1 VAL
  ; install kingcon files for 68020
  Copy >NIL: "SYSTEMDIR:Temp/_KingCON/KingCON1.3/Handler/KingCON-handler.020" "$ldir" CLONE
Else
  ; install kingcon files for 68000, default
  Copy >NIL: "SYSTEMDIR:Temp/_KingCON/KingCON1.3/Handler/KingCON-handler" "$ldir" CLONE
EndIf
Copy >NIL: "SYSTEMDIR:Temp/_KingCON/KingCON1.3/Handler/KingCON-mountlist" "$devsdir" CLONE

; delete kingcon from temp systemdir
Delete >NIL: "SYSTEMDIR:Temp/_KingCON" ALL

; install kingcon startup
Copy >NIL: "USERPACKAGEDIR:KingCON-Startup" "$sdir"

; add kingcon to user startup, if not present
Search >NIL: "$usfile" "Execute S:KingCON-Startup" quiet
IF NOT $RC EQ 0 VAL
  Type USERPACKAGEDIR:Add-KingCON-Startup >>"$usfile"
ENDIF


; end
; ---
LAB end
