; MUI v3.8
; --------
;
; Date: 2021-12-06
; Author: Henrik Noerfjand Stengaard
;
; User package for HstWB Installer to automate installation of MUI v3.8.
; If MUI v3.8 or newer version is already installed, installation of MUI ends.


; paths
set sysdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "System"`"
set muidir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "System/MUI"`"
set usfile "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S/User-Startup"`"
set muistartup "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S/MUI-Startup"`"

; set muidir, if exists, otherwise use default
set muiinstalled "0"
Assign >NIL: EXISTS MUI:
IF $RC EQ 0 VAL
  set muidir "MUI:"
  set muiinstalled "1"
ENDIF

; create mui directory, if it doesn't exist
IF NOT EXISTS "$muidir"
  MakePath >NIL: "$muidir"
ENDIF

; write install status
Echo "MUI..."

; extract mui to temp systemdir
lha -m1 -q x "USERPACKAGEDIR:mui38usr.lha" "SYSTEMDIR:Temp/_MUI/"

; install mui files
IF "$muiinstalled" EQ 1 VAL
  Copy >NIL: "SYSTEMDIR:Temp/_MUI/MUI/#?" "$muidir" CLONE ALL
Else
  Copy >NIL: "SYSTEMDIR:Temp/_MUI/MUI#?" "$sysdir" CLONE ALL
EndIf

; create user startup, if it doesn't exist
IF NOT EXISTS "$usfile"
  Echo NOLINE "" >"$usfile"
ENDIF

; create mui startup, if not present
IF NOT EXISTS "$muistartup"
  Echo "if exists *"$muidir*"" >"$muistartup"
  Echo "  assign MUI: *"$muidir*"" >>"$muistartup"
  Echo "  if exists MUI:Libs" >>"$muistartup"
  Echo "    assign add LIBS: MUI:Libs" >>"$muistartup"
  Echo "  endif" >>"$muistartup"
  Echo "  if exists MUI:Locale" >>"$muistartup"
  Echo "    assign add LOCALE: MUI:Locale" >>"$muistartup"
  Echo "  endif" >>"$muistartup"
  Echo "  version >nil: exec.library 39" >>"$muistartup"
  Echo "  if not warn" >>"$muistartup"
  Echo "    if exists MUI:Docs" >>"$muistartup"
  Echo "      if exists HELP:dummy ; do not remove" >>"$muistartup"
  Echo "      endif                ; this entry!" >>"$muistartup"
  Echo "      assign add HELP: MUI:Docs" >>"$muistartup"
  Echo "    endif" >>"$muistartup"
  Echo "  endif" >>"$muistartup"
  Echo "endif" >>"$muistartup"
ENDIF

; set mui assign to 1, if user startup contains "assign" and "MUI:" in same line
set muiassign "0"
search "$usfile" "assign" >T:_user-startup-assigns
IF $RC EQ 0 VAL
  search T:_user-startup-assigns "MUI:" >NIL:
  IF $RC EQ 0 VAL
    set muiassign "1"
  ENDIF
ENDIF

; set mui assign to 1, if user startup contains "MUI-Startup"
search "$usfile" "MUI-Startup" >NIL:
IF $RC EQ 0 VAL
  set muiassign "1"
ENDIF

; add mui startup to user startup, if mui assign is 0
IF "$muiassign" EQ 0 VAL
  Echo "IF EXISTS S:MUI-Startup" >>"$usfile"
  Echo "  Execute S:MUI-Startup" >>"$usfile"
  Echo "ENDIF" >>"$usfile"
ENDIF

; execute mui assigns, if mui installed is 0
IF "$muiinstalled" EQ 0 VAL
  Execute "$muistartup"
ENDIF

; delete temp files
Delete >NIL: "SYSTEMDIR:Temp/_MUI" ALL
Delete >NIL: T:_user-startup-assigns


; end
; ---
LAB end
