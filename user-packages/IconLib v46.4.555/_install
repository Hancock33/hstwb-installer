; iGame v2.0
; ----------
;
; Date: 2021-11-28
; Author: Henrik Noerfjand Stengaard
;
; User package for HstWB Installer to automate installation of icon library v46.4.555.


; paths
set cdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "C"`"
set libsdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "Libs"`"
set sdir "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S"`"
set bootstartup "`execute INSTALLDIR:S/CombinePath "$INSTALLDIR" "S/Boot-Startup"`"

; select icon library cpu version
set cpu `RequestChoice "IconLib version" "Select icon.library CPU version to install" "68020|68000|Default"` 

; write install status
echo "IconLib..."

; extract IconLib to temp systemdir
lha -m1 -q x "USERPACKAGEDIR:IconLib_46.4.lha" "SYSTEMDIR:Temp/_IconLib/"

; install IconLib files
If "$cpu" EQ 1 VAL
  Copy >NIL: "SYSTEMDIR:Temp/_IconLib/IconLib_46.4/Libs/68020/icon.library" "$libsdir" CLONE
Else
  Copy >NIL: "SYSTEMDIR:Temp/_IconLib/IconLib_46.4/Libs/68000/icon.library" "$libsdir" CLONE
EndIf
Copy >NIL: "SYSTEMDIR:Temp/_IconLib/IconLib_46.4/ThirdParty/LoadResident/LoadResident" "$cdir" CLONE
Copy >NIL: "SYSTEMDIR:Temp/_IconLib/IconLib_46.4/ThirdParty/RemLib/RemLib" "$cdir" CLONE

; delete IconLib from temp systemdir
Delete >NIL: "SYSTEMDIR:Temp/_IconLib" ALL

; install modules startup
Copy >NIL: USERPACKAGEDIR:Modules-Startup "$sdir"

; add boot startup to startup sequence, if not present
If Exists "$startupsequence"
  If Not Exists "$startupsequence.IconLib"
    Copy >NIL: "$startupsequence" "$startupsequence.IconLib"
  EndIf

  Search >NIL: "$startupsequence" "Execute S:Boot-Startup" quiet
  IF NOT $RC EQ 0 VAL
    Copy >NIL: USERPACKAGEDIR:Add-Boot-Startup T:_Startup-Sequence
    Type "$startupsequence" >>T:_Startup-Sequence
    Copy >NIL: T:_Startup-Sequence "$startupsequence"
    Delete >NIL: T:_Startup-Sequence
  ENDIF
Else
  Copy >NIL: USERPACKAGEDIR:Add-Boot-Startup "$startupsequence"
EndIf

; add modules startup to boot startup, if not present
If Exists "$bootstartup"
  Search >NIL: "$bootstartup" "Execute S:Modules-Startup" quiet
  IF NOT $RC EQ 0 VAL
    Type USERPACKAGEDIR:Add-Modules-Startup >>"$bootstartup"
  ENDIF
Else
  Copy USERPACKAGEDIR:Add-Modules-Startup "$bootstartup"
EndIf


; end
; ---
LAB end
