.KEY kickstartmenu/a
.BRA {
.KET }

; Build Kickstart Menu
; --------------------
;
; Date: 2021-10-27
; Author: Henrik Noerfjand Stengaard


; kickstart menu files
echo "" NOLINE >T:_kickstartdefault
echo "" NOLINE >T:_kickstartoption
echo "" NOLINE >T:_kickstartmenu
echo "" NOLINE >T:_kickstartroms

; kickstart menu lab
echo "LAB kickstartmenu" >>T:_kickstartmenu
echo "set kickstartromcount *"0*"`" >>T:_kickstartmenu  
echo "echo *"*" NOLINE >T:kickstartmenu" >>T:_kickstartmenu

; set menu option and kickstart rom count
set menuoption "0"
set kickstartroms "0"


; kickstart 3.1 40.068 a4000 rom
IF EXISTS INSTALLDIR:Prefs/Kick40068-A4000-Rom
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart 3.1 40.068 A4000 rom file detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; kickstart 3.1 40.068 a1200 rom
IF EXISTS INSTALLDIR:Prefs/Kick40068-A1200-Rom
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart 3.1 40.068 A1200 rom file detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; kickstart 3.1 40.063 a600 rom
IF EXISTS INSTALLDIR:Prefs/Kick40063-A600-Rom
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart 3.1 40.063 A600 rom file detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; kickstart 1.3 34.5 a500 rom
IF EXISTS INSTALLDIR:Prefs/Kick34005-A500-Rom
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart 1.3 34.5 A500 rom file detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; kickstart 1.2 33.180 a500 rom
IF EXISTS INSTALLDIR:Prefs/Kick33180-A500-Rom
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart 1.2 33.180 A500 rom file detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; kickstart rom key, cloanto amiga forever
IF EXISTS INSTALLDIR:Prefs/AF-Rom-Key
  set kickstartroms `eval $kickstartroms + 1`
  echo "echo *"Kickstart rom key file, Cloanto Amiga Forever detected*" >>T:kickstartmenu" >>T:_kickstartroms
ENDIF

; install option
set menuoption `eval $menuoption + 1`
echo "" >>T:_kickstartoption
echo "IF *"*$kickstartoption*" EQ $menuoption VAL" >>T:_kickstartoption
echo "  set confirm *`RequestChoice *"Kickstart installation*" *"Do you want to continue with installation of**N$kickstartroms Kickstart rom file(s)?*" *"Yes|No*"*`" >>T:_kickstartoption
echo "  IF *"*$confirm*" EQ 1 VAL" >>T:_kickstartoption
echo "    SKIP kickstartinstall" >>T:_kickstartoption
echo "  ENDIF" >>T:_kickstartoption
echo "  SKIP BACK kickstartmenu" >>T:_kickstartoption
echo "ENDIF" >>T:_kickstartoption
echo "echo *"Install $kickstartroms Kickstart rom file(s)*" >>T:kickstartmenu" >>T:_kickstartmenu

; skip option
set menuoption `eval $menuoption + 1`
echo "" >>T:_kickstartoption
echo "IF *"*$kickstartoption*" EQ $menuoption VAL" >>T:_kickstartoption
echo "  set confirm *`RequestChoice *"Skip Kickstart installation*" *"Do you want to skip installation of**NKickstart rom files?*" *"Yes|No*"*`" >>T:_kickstartoption
echo "  IF *"*$confirm*" EQ 1 VAL" >>T:_kickstartoption
echo "    SKIP skipkickstart" >>T:_kickstartoption
echo "  ENDIF" >>T:_kickstartoption
echo "  SKIP BACK kickstartmenu" >>T:_kickstartoption
echo "ENDIF" >>T:_kickstartoption
echo "echo *"Skip Kickstart rom files installation*" >>T:kickstartmenu" >>T:_kickstartmenu

; add splitter
set menuoption `eval $menuoption + 1`
echo "echo *"--------------------------------------------------*" >>T:kickstartmenu" >>T:_kickstartmenu

; kickstart rom options
Type T:_kickstartroms >>T:_kickstartmenu
set menuoption `eval $menuoption + $kickstartroms`

; add splitter
IF "$kickstartroms" GT 0 VAL
  set menuoption `eval $menuoption + 1`
  echo "echo *"--------------------------------------------------*" >>T:kickstartmenu" >>T:_kickstartmenu
ENDIF

; help option
set menuoption `eval $menuoption + 1`
echo "" >>T:_kickstartoption
echo "IF *"*$kickstartoption*" EQ $menuoption VAL" >>T:_kickstartoption
echo "  Lister *"INSTALLDIR:Help/Kickstart-Installation.txt*" >NIL:" >>T:_kickstartoption
echo "  SKIP BACK kickstartmenu" >>T:_kickstartoption
echo "ENDIF" >>T:_kickstartoption
echo "echo *"Help*" >>T:kickstartmenu" >>T:_kickstartmenu

; view detect kickstart log option
set menuoption `eval $menuoption + 1`
echo "" >>T:_kickstartoption
echo "IF *"*$kickstartoption*" EQ $menuoption VAL" >>T:_kickstartoption
echo "  IF EXISTS *"INSTALLDIR:Temp/Detect-Kickstart.log*"" >>T:_kickstartoption
echo "    Lister *"INSTALLDIR:Temp/Detect-Kickstart.log*" >NIL:" >>T:_kickstartoption
echo "  ELSE" >>T:_kickstartoption
echo "    RequestChoice *"Error*" *"Detect Kickstart log doesn't exist!*" *"OK*" >NIL:" >>T:_kickstartoption
echo "  ENDIF" >>T:_kickstartoption
echo "  SKIP BACK kickstartmenu" >>T:_kickstartoption
echo "ENDIF" >>T:_kickstartoption
echo "echo *"View Detect Kickstart log*" >>T:kickstartmenu" >>T:_kickstartmenu

; restart detect kickstart option
set menuoption `eval $menuoption + 1`
echo "" >>T:_kickstartoption
echo "IF *"*$kickstartoption*" EQ $menuoption VAL" >>T:_kickstartoption
echo "  set confirm *`RequestChoice *"Kickstart installation*" *"Do you want to restart detect kickstart?*" *"Yes|No*"*`" >>T:_kickstartoption
echo "  IF *"*$confirm*" EQ 1 VAL" >>T:_kickstartoption
echo "    echo *"*" NOLINE >>*"INSTALLDIR:Prefs/Restart-Detect-Kickstart*"" >>T:_kickstartoption
echo "    SKIP end" >>T:_kickstartoption
echo "  ENDIF" >>T:_kickstartoption
echo "ENDIF" >>T:_kickstartoption
echo "echo *"Restart Detect Kickstart*" >>T:kickstartmenu" >>T:_kickstartmenu

; show kickstart installation menu
echo "" >>T:_kickstartmenu
echo "set kickstartoption *"*"" >>T:_kickstartmenu
echo "set kickstartoption *"*`RequestList TITLE=*"Kickstart installation*" LISTFILE=*"T:kickstartmenu*" WIDTH=640 LINES=24*`*"" >>T:_kickstartmenu

; add kickstart options to kickstart menu
echo "" >>T:_kickstartmenu
type T:_kickstartoption >>T:_kickstartmenu

echo "" >>T:_kickstartmenu
echo "SKIP BACK kickstartmenu" >>T:_kickstartmenu
echo "" >>T:_kickstartmenu
echo "; kickstart install" >>T:_kickstartmenu
echo "; -----------------" >>T:_kickstartmenu
echo "LAB kickstartinstall" >>T:_kickstartmenu
echo "set installkickstart *"0*"" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/AF-Rom-Key" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-AF-Rom-Key*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/Kick33180-A500-Rom" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kick33180-A500-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/Kick34005-A500-Rom" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kick34005-A500-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/Kick40063-A600-Rom" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kick40063-A600-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/Kick40068-A1200-Rom" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kick40068-A1200-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu

echo "IF EXISTS INSTALLDIR:Prefs/Kick40068-A4000-Rom" >>T:_kickstartmenu
echo "  set installkickstart *"1*"" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kick40068-A4000-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu
echo "IF *"*$installkickstart*" EQ 1 VAL" >>T:_kickstartmenu
echo "  echo *"*" NOLINE >*"INSTALLDIR:Prefs/Install-Kickstart-Rom*"" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu
echo "" >>T:_kickstartmenu
echo "SKIP end" >>T:_kickstartmenu
echo "" >>T:_kickstartmenu
echo "LAB skipkickstart" >>T:_kickstartmenu
echo "IF EXISTS INSTALLDIR:Prefs/Install-Kickstart-Rom" >>T:_kickstartmenu
echo "  Delete >NIL: INSTALLDIR:Prefs/Install-Kickstart-Rom" >>T:_kickstartmenu
echo "ENDIF" >>T:_kickstartmenu
echo "" >>T:_kickstartmenu
echo "LAB end" >>T:_kickstartmenu
echo "" >>T:_kickstartmenu

; build kickstart menu
type T:_kickstartdefault >"{kickstartmenu}"
type T:_kickstartmenu >>"{kickstartmenu}"


; end
; ---

Delete >NIL: T:_kickstartoption
Delete >NIL: T:_kickstartdefault
Delete >NIL: T:_kickstartmenu
Delete >NIL: T:_kickstartroms
