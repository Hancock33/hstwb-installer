.KEY file/a,dir/a
.BRA {
.KET }

; Copy Kickstart Rom
; ------------------
;
; Date: 2021-10-24
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to identify and copy valid Kickstart rom files.

; goto end, if file doesn't exist
IF NOT EXISTS "{file}"
  SKIP end
ENDIF

; add file to log
echo "File = '{file}'" >>INSTALLDIR:Temp/Kickstart-Rom.log

; goto end, if dir doesn't exist
IF NOT EXISTS "{dir}"
  SKIP end
ENDIF

; escape file
echo "{file}" >T:_escapedfile

set kickstartromkey "0"
IF NOT "`FSearch "T:_escapedfile" TXT=":rom.key"`" EQ ""
  set kickstartromkey "1"
ENDIF
IF NOT "`FSearch "T:_escapedfile" TXT="/rom.key"`" EQ ""
  set kickstartromkey "1"
ENDIF

; escape file continued
Execute INSTALLDIR:S/Escape-Filename T:_escapedfile
set escapedfile "`type T:_escapedfile`"
Delete >NIL: T:_escapedfile

set validkickstartrom "0"
set rommd5result ""

; get md5 from file
rommd5 "$escapedfile" >T:_md5
IF $RC EQ 0 VAL
  set validkickstartrom "1"

  md5 "$escapedfile" >T:_encryptedmd5
  set encryptedmd5 ""
  set encryptedmd5 "`type T:_encryptedmd5`"
  Delete >NIL: T:_encryptedmd5

  ; add encrypted md5 to log
  echo "MD5 = '$encryptedmd5' (encrypted rom md5)" >>INSTALLDIR:Temp/Kickstart-Rom.log

  set rommd5result " (ok, decrypted using key.rom)"
ENDIF
IF $RC EQ 5 VAL
  set validkickstartrom "1"
  set rommd5result " (ok, not encrypted)"
ENDIF
IF $RC EQ 10 VAL
  set rommd5result " (error, key.rom not found)"
ENDIF

; get md5 for file
set md5 ""
set md5 "`type T:_md5`"
Delete >NIL: T:_md5

; add md5 to log
echo "MD5 = '$md5'$rommd5result" >>INSTALLDIR:Temp/Kickstart-Rom.log

; copy kickstart rom key, if file is cloanto amiga forever rom key
IF "$kickstartromkey" EQ 1 VAL
  echo "Kickstart Rom Key, Cloanto Amiga Forever detected" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "rom.key"
  SKIP copyromfile
ENDIF

; skip to end, if kickstart rom is not valid
IF "$validkickstartrom" EQ 0 VAL
  SKIP end
ENDIF


; kickstart md5
; -------------

; copy kickstart 3.1 40.068 a4000 rom, if file md5 matches
IF $md5 eq "9bdedde6a4f33555b4a270c8ca53297d"
  echo "Kickstart 3.1 40.068 A4000 rom, detected using MD5" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "kick40068.A4000"
  SKIP copyromfile
ENDIF

; copy kickstart 3.1 40.068 a1200 rom, if file md5 matches
IF $md5 eq "646773759326fbac3b2311fd8c8793ee"
  echo "Kickstart 3.1 40.068 A1200 rom, detected using MD5" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "kick40068.A1200"
  SKIP copyromfile
ENDIF

; copy kickstart 3.1 40.063 a600 rom, if file md5 matches
IF $md5 eq "e40a5dfb3d017ba8779faba30cbd1c8e"
  echo "Kickstart 3.1 40.063 A600 rom, detected using MD5" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "kick40063.A600"
  SKIP copyromfile
ENDIF

; copy kickstart 1.3 34.5 a500 rom, if file md5 matches
IF $md5 eq "82a21c1890cae844b3df741f2762d48d"
  echo "Kickstart 1.3 34.5 A500 rom, detected using MD5" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "kick34005.A500"
  SKIP copyromfile
ENDIF

; copy kickstart 1.2 33.180 a500 rom, if file md5 matches
IF $md5 eq "85ad74194e87c08904327de1a9443b7a"
  echo "Kickstart 1.2 33.180 A500 rom, detected using MD5" >>INSTALLDIR:Temp/Kickstart-Rom.log
  set kickstartromfile "kick33180.A500"
  SKIP copyromfile
ENDIF

SKIP end


; copy rom file
; -------------
LAB copyromfile

; add copy rom file to log
echo "Copying to '{dir}/$kickstartromfile'" >>INSTALLDIR:Temp/Kickstart-Rom.log

; make rom file deletable, if it exists
IF EXISTS "{dir}/$kickstartromfile"
  Protect >NIL: "{dir}/$kickstartromfile" +prwed
ENDIF

; copy rom file
Copy >>INSTALLDIR:Temp/Kickstart-Rom.log "$escapedfile" "{dir}/$kickstartromfile"


; end
; ---
LAB end
