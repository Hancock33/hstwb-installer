; Detect Kickstart Rom
; --------------------
;
; Date: 2021-10-24
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect if Cloanto Amiga Forever and original Kickstart rom files are present.


; add detect kickstart to log
echo "Kickstart rom" >INSTALLDIR:Temp/Kickstart-Rom.log
echo "-------------" >>INSTALLDIR:Temp/Kickstart-Rom.log
echo "Detecting Kickstart rom files in KICKSTARTDIR:..." >>INSTALLDIR:Temp/Kickstart-Rom.log

; create kickstart temp directory, if it doesn't exist
IF NOT EXISTS "INSTALLDIR:Temp/Kickstart/"
  MakePath >NIL: "INSTALLDIR:Temp/Kickstart"
ENDIF

; create script to copy kickstart rom files
List KICKSTARTDIR: PAT=#? ALL FILES LFORMAT "Execute INSTALLDIR:S/Kickstart/Copy-Kickstart-Rom *"%p%n*" *"INSTALLDIR:Temp/Kickstart*"" >T:_copy_kickstart_roms

; run copy kickstart roms
FailAt 21
Execute T:_copy_kickstart_roms
Delete >NIL: T:_copy_kickstart_roms

; create prefs, if kickstart 1.2 (33.180) a500 rom is present
set kick33180a500 "0"
IF EXISTS "INSTALLDIR:Temp/Kickstart/kick33180.A500"
  set kick33180a500 "1"
  echo "" NOLINE >INSTALLDIR:Prefs/Kick33180-A500-Rom
ELSE
  echo "Kickstart 1.2 (33.180) A500 rom doesn't exist!" >>INSTALLDIR:Temp/Kickstart-Rom.log
ENDIF

; create prefs, if kickstart 1.3 (34.5) a500 rom is present
set kick34005a500 "0"
IF EXISTS "INSTALLDIR:Temp/Kickstart/kick34005.A500"
  set kick34005a500 "1"
  echo "" NOLINE >INSTALLDIR:Prefs/Kick34005-A500-Rom
ELSE
  echo "Kickstart 1.3 (34.5) A500 rom doesn't exist!" >>INSTALLDIR:Temp/Kickstart-Rom.log
ENDIF

; create prefs, if kickstart 3.1 (40.063) a600 rom is present
set kick40063a600 "0"
IF EXISTS "INSTALLDIR:Temp/Kickstart/kick40063.A600"
  set kick40063a600 "1"
  echo "" NOLINE >INSTALLDIR:Prefs/Kick40063-A600-Rom
ELSE
  echo "Kickstart 3.1 (40.063) A600 rom doesn't exist!" >>INSTALLDIR:Temp/Kickstart-Rom.log
ENDIF

; create prefs, if kickstart 3.1 (40.068) a1200 rom is present
set kick40068a1200 "0"
IF EXISTS "INSTALLDIR:Temp/Kickstart/kick40068.A1200"
  set kick40068a1200 "1"
  echo "" NOLINE >INSTALLDIR:Prefs/Kick40068-A1200-Rom
ELSE
  echo "Kickstart 3.1 (40.068) A1200 rom doesn't exist!" >>INSTALLDIR:Temp/Kickstart-Rom.log
ENDIF

; create prefs, if kickstart 3.1 (40.068) a4000 rom is present
set kick40068a4000 "0"
IF EXISTS "INSTALLDIR:Temp/Kickstart/kick40068.A4000"
  set kick40068a4000 "1"
  echo "" NOLINE >INSTALLDIR:Prefs/Kick40068-A4000-Rom
ELSE
  echo "kickstart 3.1 (40.068) A4000 rom doesn't exist!" >>INSTALLDIR:Temp/Kickstart-Rom.log
ENDIF

; create prefs, if cloanto amiga forever kickstart rom key
IF EXISTS "INSTALLDIR:Temp/Kickstart/rom.key"
  echo "" NOLINE >INSTALLDIR:Prefs/AF-Rom-Key
ENDIF


; end
; ---
LAB end
