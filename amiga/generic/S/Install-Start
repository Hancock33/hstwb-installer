; Install Start
; -------------
;
; Date: 2021-11-02
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to start installation of Large Harddisk, Amiga OS 3.9, 3.2, 3.1.4 or 3.1, Kickstarts and packages.


; detect pal or ntsc monitor
INSTALLDIR:C/paltest >T:_paltest
search >NIL: T:_paltest "PAL  "
IF $RC EQ 0 VAL
  echo "" NOLINE >"INSTALLDIR:Prefs/Pal-Monitor"
ENDIF
Delete >NIL: T:_paltest

; Copy large harddisk, if install large harddisk exists
IF EXISTS "INSTALLDIR:Install-LargeHarddisk"
  copy >NIL: "INSTALLDIR:Install-LargeHarddisk/" TO "SYSTEMDIR:" ALL
ENDIF

; mount ff0: device using mountlist
assign FMS: SYSTEMDIR:Temp
Mount FF0: FROM "INSTALLDIR:Devs/Mountlist"

; wait for mount
Wait 2


; get amiga os install
set amigaosinstall ""
set amigaosinstall "`type INSTALLDIR:Prefs/Install-Amiga-OS`"

; run install amiga os 3.9 emergency boot, if prefs exists
IF "$amigaosinstall" EQ "Amiga-OS-390"
  execute "INSTALLDIR:S/Amiga-OS-3.9/Install-Emergency-Boot"
ELSE
  ; run install amiga os 3.2, if prefs exists
  IF "$amigaosinstall" EQ "Amiga-OS-32-ADF"
    execute "INSTALLDIR:S/Amiga-OS-3.2/Install-Amiga-OS-3.2"
  ELSE
    ; run install amiga os 3.1.4, if prefs exists
    IF "$amigaosinstall" EQ "Amiga-OS-314-ADF"
        execute "INSTALLDIR:S/Amiga-OS-3.1.4/Install-Amiga-OS-3.1.4"
    ELSE
        ; run install amiga os 3.1, if prefs exists
        IF "$amigaosinstall" EQ "Amiga-OS-310-ADF"
          execute "INSTALLDIR:S/Amiga-OS-3.1/Install-Amiga-OS-3.1"
        ELSE
          ; run detect amiga os disk
          execute "INSTALLDIR:S/Detect-Amiga-OS-Disk"
        ENDIF
    ENDIF
  ENDIF
ENDIF

; install kickstart, if prefs exists
IF EXISTS "INSTALLDIR:Prefs/Install-Kickstart-Rom"
  execute "INSTALLDIR:S/Kickstart/Install-Kickstart-Rom"
ENDIF
