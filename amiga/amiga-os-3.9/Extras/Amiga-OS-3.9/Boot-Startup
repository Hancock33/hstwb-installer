; Boot Startup
; ------------
;
; Date: 2021-11-02
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to boot Amiga OS 3.9 startup which load patched scsi.device and
; enable or disable 680x0 libraries depending on cpu model


; set failat to 21
failat 21

; skip mmu libraries patch, if disable mmu libraries patch prefs exist
IF EXISTS SYS:Prefs/Env-Archive/Disable-MMULibs-Patch
  SKIP patchscsi
ENDIF

; set cpu to 68000 by default
set cpumodel "68000"

; set cpu model to 68020, if test cpu returns 68020 is present
TestCPU 68020
IF $RC EQ 0 VAL
  set cpumodel "68020"
ENDIF

; set cpu model to 68030, if test cpu returns 68030 is present
TestCPU 68030
IF $RC EQ 0 VAL
  set cpumodel "68030"
ENDIF

; set cpu model to 68040, if test cpu returns 68040 is present
TestCPU 68040
IF $RC EQ 0 VAL
  set cpumodel "68040"
ENDIF

; set cpu model to 68060, if test cpu returns 68060 is present
TestCPU 68060
IF $RC EQ 0 VAL
  set cpumodel "68060"
ENDIF

; disable 68020 library, ifenabled
IF EXISTS Libs:68020.library
  Rename >NIL: Libs:68020.library Libs:68020.library_disabled
ENDIF

; disable 68030 library, if enabled
IF EXISTS Libs:68030.library
  Rename >NIL: Libs:68030.library Libs:68030.library_disabled
ENDIF

; disable 68040 library, if enabled
IF EXISTS Libs:68040.library
  Rename >NIL: Libs:68040.library Libs:68040.library_disabled
ENDIF

; disable 68060 library, if enabled
IF EXISTS Libs:68060.library
  Rename >NIL: Libs:68060.library Libs:68060.library_disabled
ENDIF

; enable detected cpu model, if disabled
IF EXISTS Libs:$cpumodel.library_disabled
  Rename >NIL: Libs:$cpumodel.library_disabled Libs:$cpumodel.library
ENDIF


; patch scsi device
LAB patchscsi

; load patched scsi.device v43.45, if it exists
IF EXISTS DEVS:scsi.device
  ; Load scsi.device, if current loaded scsi.device is less than v43.45
  Version "scsi.device" 43 45
  IF WARN
    LoadModule DEVS:scsi.device
  ENDIF
ENDIF
