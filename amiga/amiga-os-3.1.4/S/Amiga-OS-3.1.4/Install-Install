; Install Install
; ---------------
;
; Date: 2019-08-14
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to install from amiga os 3.1.4 install floppy disk or adf file.


; copy disk.info
IF EXISTS "SYSTEMDIR:Disk.info"
  Protect >NIL: "SYSTEMDIR:Disk.info" +prwed
ENDIF
Copy >NIL: "$amigaosdisk:Update/Disk.info" "SYSTEMDIR:" CLONE

Copy >NIL: "$amigaosdisk:HDTools/(hd|bru)#?" "SYSTEMDIR:Tools" CLONE
Copy >NIL: "$amigaosdisk:Installer" "SYSTEMDIR:System" CLONE

; copy workbench.library
IF EXISTS "SYSTEMDIR:Libs/workbench.library"
  Protect >NIL: "SYSTEMDIR:Libs/workbench.library" +prwed
ENDIF
Copy >NIL: "$amigaosdisk:Libs/workbench.library" "SYSTEMDIR:Libs" CLONE

; copy icon.library
IF EXISTS "SYSTEMDIR:Libs/icon.library"
  Protect >NIL: "SYSTEMDIR:Libs/icon.library" +prwed
ENDIF
Copy >NIL: "$amigaosdisk:Libs/icon.library" "SYSTEMDIR:Libs" CLONE

; copy startup-sequence
Copy "$amigaosdisk:Update/Startup-HardDrive" "SYSTEMDIR:S" CLONE
IF EXISTS "SYSTEMDIR:S/Startup-Sequence"
  Protect >NIL: "SYSTEMDIR:S/Startup-Sequence" +prwed
  Delete >NIL: "SYSTEMDIR:S/Startup-Sequence"
ENDIF
Copy >NIL: "SYSTEMDIR:S/Startup-HardDrive" "SYSTEMDIR:S/Startup-Sequence"

; copy fastfilesystem
IF EXISTS "SYSTEMDIR:L/FastFileSystem"
  Protect >NIL: "SYSTEMDIR:L/FastFileSystem" +prwed
ENDIF
Copy >NIL: "$amigaosdisk:L/FastFileSystem" "SYSTEMDIR:L" CLONE


Assign C: SYSTEMDIR:C ADD

IF EXISTS SYSTEMDIR:S
  Assign S: SYSTEMDIR:S ADD
ENDIF

IF EXISTS SYSTEMDIR:L
  Assign L: SYSTEMDIR:L ADD
ENDIF

IF EXISTS SYSTEMDIR:Libs
  Assign LIBS: SYSTEMDIR:Libs ADD
ENDIF

IF EXISTS SYSTEMDIR:Devs
  Assign DEVS: SYSTEMDIR:Devs ADD
ENDIF

IF EXISTS SYSTEMDIR:Fonts
  Assign Fonts: SYSTEMDIR:Fonts ADD
ENDIF

IF EXISTS SYSTEMDIR:Prefs/Env-Archive
  Assign ENVARC: SYSTEMDIR:Prefs/Env-Archive
ENDIF

; run update wb files
Assign >NIL: NEWWB: SYSTEMDIR:
"$amigaosdisk:C/UpdateWBFiles"


; Clean up
; --------

Resident "$amigaosdisk:C/IconPos" PURE

IconPos >NIL: "SYSTEMDIR:Prefs"     12 20
IconPos >NIL: "SYSTEMDIR:Prefs/Printer"  160 48

IconPos >NIL: "SYSTEMDIR:Utilities" 98 4
IconPos >NIL: "SYSTEMDIR:Utilities/Clock" 91 11
IconPos >NIL: "SYSTEMDIR:Utilities/MultiView" 11 11

IconPos >NIL: "SYSTEMDIR:Tools"                           98 38
IconPos >NIL: "SYSTEMDIR:Tools/IconEdit"                 111 45
IconPos >NIL: "SYSTEMDIR:Tools/HDToolBox"                 202 4
IconPos >NIL: "SYSTEMDIR:Tools/Commodities/Blanker"        8 84
IconPos >NIL: "SYSTEMDIR:Tools/Commodities/ClickToFront"  99 4
IconPos >NIL: "SYSTEMDIR:Tools/Commodities/CrossDOS"      99 44
IconPos >NIL: "SYSTEMDIR:Tools/Commodities/Exchange"       8  4
IconPos >NIL: "SYSTEMDIR:Tools/Commodities/FKey"          99 84

IconPos >NIL: "SYSTEMDIR:System"    184 4
IconPos >NIL: "SYSTEMDIR:WBStartup" 184 38
IconPos >NIL: "SYSTEMDIR:Devs"      270 4
Copy >NIL: "SYSTEMDIR:Devs.info" "SYSTEMDIR:Storage.info" CLONE
IconPos >NIL: "SYSTEMDIR:Storage"   270 38 DXPOS 480 DYPOS 77 DWIDTH 107 DHEIGHT 199
IconPos >NIL: "SYSTEMDIR:Storage/Monitors"   10 106 DXPOS 480 DYPOS 77 DWIDTH 107 DHEIGHT 199
IconPos >NIL: "SYSTEMDIR:Storage/Printers"   10 140 DXPOS 480 DYPOS 77 DWIDTH 107 DHEIGHT 199
IconPos >NIL: "SYSTEMDIR:Expansion" 356 20
IconPos >NIL: "SYSTEMDIR:Disk"      DXPOS 28 DYPOS 29 DWIDTH 462 DHEIGHT 93

Protect >NIL: "SYSTEMDIR:C/Execute" ph add

; backup old icons and copy glowicons to system directory, if glow icons prefs exists
IF EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.1.4-GlowIcons"
  IF EXISTS "SYSTEMDIR:Icons/GlowIcons"
    echo "Creating old icons backup..."
    Lha -q -e -r -x a "SYSTEMDIR:Icons/OldIcons/OldIcons-Backup.lha" "SYSTEMDIR:" #?.info

    echo "Installing glow icons..."
    Copy >NIL: "SYSTEMDIR:Icons/GlowIcons" "SYSTEMDIR:" ALL CLONE
  ENDIF
ENDIF

; create prefs env archive sys directory
MakePath >NIL: "SYSTEMDIR:Prefs/Env-Archive/Sys"

; copy workbench prefs to use best icon quality and no icon border
IF EXISTS "INSTALLDIR:Extras/Prefs/Workbench.prefs"
  Copy >NIL: "INSTALLDIR:Extras/Prefs/Workbench.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/Workbench.prefs" CLONE
ENDIF

IF EXISTS "INSTALLDIR:Prefs/Pal-Monitor"
  IF EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.1.4-Screenmode"
    IF EXISTS "INSTALLDIR:Extras/Prefs/ScreenMode_PAL_640x512.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Prefs/ScreenMode_PAL_640x512.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ELSE
    IF EXISTS "INSTALLDIR:Extras/Prefs/ScreenMode_PAL_640x256.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Prefs/ScreenMode_PAL_640x256.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ENDIF
ELSE
  IF EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.1.4-Screenmode"
    IF EXISTS "INSTALLDIR:Extras/Prefs/ScreenMode_NTSC_640x400.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Prefs/ScreenMode_NTSC_640x400.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ELSE
    IF EXISTS "INSTALLDIR:Extras/Prefs/ScreenMode_NTSC_640x200.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Prefs/ScreenMode_NTSC_640x200.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ENDIF
ENDIF

; set harddisk icon
set harddiskicon ""
IF EXISTS "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info"
  set harddiskicon "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info"
ELSE
  IF EXISTS "SYSTEMDIR:Disk.info"
    set harddiskicon "SYSTEMDIR:Disk.info"
  ENDIF
ENDIF

; update disk icon, if harddisk icon exists
IF NOT "$harddiskicon" EQ ""
  ; update dh1 disk icon, if dh1: device exists
  Assign >NIL: EXISTS DH1:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH1:Disk.info"
  ENDIF

  ; update dh2 disk icon, if dh2: device exists
  Assign >NIL: EXISTS DH2:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH2:Disk.info"
  ENDIF
ENDIF

; install 680x0 libraries required for cpus and accelerator cards
MakePath >NIL: "SYSTEMDIR:Temp"
lha -m1 x "INSTALLDIR:Extras/MMULib.lha" "SYSTEMDIR:Temp/" >NIL:
Copy >NIL: "SYSTEMDIR:Temp/MMULib/Libs/680?0.library" "SYSTEMDIR:Libs" CLONE
Copy >NIL: "SYSTEMDIR:Temp/MMULib/Libs/mmu.library" "SYSTEMDIR:Libs" CLONE
Delete >NIL: "SYSTEMDIR:Temp/MMULib" ALL
