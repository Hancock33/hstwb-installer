; $VER: Startup-Sequence_LD 47.4 (24.04.2020)
; Startup-Sequence for low-density workbench floppy

Version exec.library version 47 >NIL:
If Warn
  If Exists L:System-Startup
    Echo ""
    Echo " Forcing new ROM modules. A reboot will happen next."
    Echo " Please wait..."
    LoadModule L:System-Startup ROMUPDATE DOWNGRADE
  Else
    Echo ""
    Echo " This disk must be booted from Kickstart ROM 3.2 (V47)"
    Echo " or from the 'Modules' disk that matches your hardware."
    Echo ""
    Quit
  EndIf
EndIf

SetPatch QUIET
CPU CHECKINSTALL

Version >NIL:
AddBuffers >NIL: DF0: 15
FailAt 21

MakeDir RAM:T RAM:Clipboards
MakeLink RAM:ENV to ENVARC: Force

Resident >NIL: C:Assign PURE

Assign >NIL: ENV: RAM:ENV
Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

IF NOT EXISTS SYS:L
  Assign >NIL: L: Extras3.2:L DEFER
EndIF

IF NOT EXISTS SYS:Fonts
  Assign FONTS:
EndIF

BindDrivers
Mount >NIL: DEVS:DOSDrivers/~(#?.info)

IF EXISTS DEVS:Monitors
  IF EXISTS DEVS:Monitors/VGAOnly
    DEVS:Monitors/VGAOnly
  EndIF

  C:List >NIL: DEVS:Monitors/~(#?.info|VGAOnly) TO T:M LFORMAT "DEVS:Monitors/%s"
  Execute T:M
  C:Delete >NIL: T:M
EndIF

SetEnv Language "english"
SetEnv Workbench $Workbench
SetEnv Kickstart $Kickstart
UnSet Workbench
UnSet Kickstart

AddDataTypes REFRESH QUIET
IPrefs
ConClip

Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities

IF EXISTS S:User-Startup
  Execute S:User-Startup
EndIF

Resident Assign REMOVE

LoadWB
EndCLI >NIL:
