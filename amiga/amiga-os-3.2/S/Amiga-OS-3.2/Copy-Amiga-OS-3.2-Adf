.KEY file/a,dir/a
.BRA {
.KET }

; Copy Amiga OS 3.2 Adf
; ---------------------
;
; Date: 2021-12-29
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to identify and copy valid Amiga OS 3.2 adf files.


; goto end, if file doesn't exist
IF NOT EXISTS "{file}"
  SKIP end
ENDIF

; add file to log
echo "File = '{file}'" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log

; goto end, if dir doesn't exist
IF NOT EXISTS "{dir}"
  SKIP end
ENDIF

; escape file
echo "{file}" >T:_escapedfile
Execute INSTALLDIR:S/Escape-Filename T:_escapedfile
set escapedfile "`type T:_escapedfile`"
Delete >NIL: T:_escapedfile

; skip to end, if amiga os md5 doesn't exists
If Not Exists "INSTALLDIR:Temp/Amiga-OS-MD5"
  echo "Error: Amiga OS MD5 doesn't exist!" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  
  ; goto mount, if md5 only prefs doesn't exists
  If Not Exists INSTALLDIR:Prefs/MD5-Only
    SKIP mount
  ENDIF

  Skip end
EndIf

; find md5 for amiga os file
Search "INSTALLDIR:Temp/Amiga-OS-MD5" "{file}" NONUM >T:_amiga-os-md51
If Not $RC EQ 0 VAL
  Skip end
EndIf

; get found amiga os md5 text
sed "1q;d" T:_amiga-os-md51 >T:_amiga-os-md52
set md5 `Execute T:_amiga-os-md52`
Delete >NIL: T:_amiga-os-md5#?


; md5
; ---
LAB md5

; add md5 to log
echo "MD5 = '$md5'" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log

; skip to end, if amiga os 3.2 md5 doesn't exist
If Not Exists "INSTALLDIR:MD5/Amiga-OS-3.2/$md5"
  SKIP end
EndIf

set adffilename `Type "INSTALLDIR:MD5/Amiga-OS-3.2/$md5"`
echo "'$adffilename' detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
SKIP copyadffile

; goto end, if md5 only prefs exists
IF EXISTS INSTALLDIR:Prefs/MD5-Only
  SKIP end
ENDIF


; mount file
; ----------
LAB mount

; goto end, if file size is not 901120 bytes
IF NOT "`wc -c <"$tempadffile"`" EQ 901120 VAL
  SKIP end
ENDIF

; add mount file to log
echo "Mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log

; run mount adf
execute "INSTALLDIR:S/Mount-Adf" "$tempadffile"

; goto end, if adf is not mounted
IF NOT "$adfmounted" EQ 1 VAL
  ; run unmount adf
  execute "INSTALLDIR:S/Unmount-Adf"

  SKIP end
ENDIF

; copy amiga os 3.2 workbench file, if workbench adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Workbench"`" EQ ""
  echo "Amiga OS 3.2 Workbench adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-workbench.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 storage file, if storage adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Storage"`" EQ ""
  echo "Amiga OS 3.2 Storage adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-storage.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 locale file, if locale adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Locale"`" EQ ""
  echo "Amiga OS 3.2 Locale adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-locale.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 install file, if install adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Install"`" EQ ""
  echo "Amiga OS 3.2 Install adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-install.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 fonts file, if fonts adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Fonts"`" EQ ""
  echo "Amiga OS 3.2 Fonts adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-fonts.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 extras file, if extras adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Extras"`" EQ ""
  echo "Amiga OS 3.2 Extras adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-extras.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 modules file, if modules adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Modules"`" EQ ""
  echo "Amiga OS 3.2 Modules $modulesname adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-modules-$modulesfile.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 classes file, if classes adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Classes"`" EQ ""
  echo "Amiga OS 3.2 Classes adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-classes.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 disk doctor file, if disk doctor adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-DiskDoctor"`" EQ ""
  echo "Amiga OS 3.2 Disk Doctor adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-disk-doctor.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 glow icons file, if glow icons adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-GlowIcons"`" EQ ""
  echo "Amiga OS 3.2 Glow Icons adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-glow-icons.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 mmulibs file, if glow icons adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-MMULibs"`" EQ ""
  echo "Amiga OS 3.2 MMULibs adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-mmulibs.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2 backdrops file, if glow icons adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Backdrops"`" EQ ""
  echo "Amiga OS 3.2 Backdrops adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-32-backdrops.adf"
  SKIP copyadffile
ENDIF

; amiga os 3.2.1
; --------------

; copy amiga os 3.2.1 classes file, if classes adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Classes3.2.1"`" EQ ""
  echo "Amiga OS 3.2.1 Classes adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-321-classes.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2.1 disk doctor file, if disk doctor adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-DiskDoctor3.2.1"`" EQ ""
  echo "Amiga OS 3.2.1 Disk Doctor adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-321-disk-doctor.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2.1 locale file, if locale adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Locale3.2.1"`" EQ ""
  echo "Amiga OS 3.2.1 Locale adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-321-locale.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2.1 modules file, if modules adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Modules3.2.1"`" EQ ""
  echo "Amiga OS 3.2.1 Modules $modulesname adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-321-modules-$modulesfile.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.2.1 update file, if update adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.2/Detect-Update3.2.1"`" EQ ""
  echo "Amiga OS 3.2.1 Update adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log
  set adffilename "amiga-os-321-update.adf"
  SKIP copyadffile
ENDIF

SKIP end


; copy adf file
; -------------
LAB copyadffile

; add copy to log
echo "Copying to '{dir}/$adffilename'" >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log

; make adf file deletable, if it exists
IF EXISTS "{dir}/$adffilename"
  Protect >NIL: "{dir}/$adffilename" +prwed
ENDIF

Copy >>INSTALLDIR:Temp/Amiga-OS-32-Adf.log "$escapedfile" TO "{dir}/$adffilename"


; end
; ---
LAB end

; run unmount adf
execute "INSTALLDIR:S/Unmount-Adf"
