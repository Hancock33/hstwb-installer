; Install Locale
; --------------
;
; Date: 2021-12-07
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to install from amiga os 3.2 locale floppy disk or adf file.

If "`Which $amigaosdisk:`" EQ "Locale:"
  ; copy catalogs
  Copy >NIL: "$amigaosdisk:Catalogs" "SYSTEMDIR:Locale/Catalogs" ALL CLONE

  ; copy countries
  Copy >NIL: "$amigaosdisk:Countries" "SYSTEMDIR:Locale/Countries" ALL CLONE

  ; uncompress support fonts .z files
  list "$amigaosdisk:Support/Fonts" ALL FILES PAT=#?.Z LFORMAT="IF NOT EXISTS *"X_%f*"*NMakePath >NIL: *"X_%f*"*NENDIF*Ngunzip -d *"%f%n*" -c >*"X_%f%m*"" >T:_uncompress
  rep T:_uncompress "X_$amigaosdisk:Support/" "SYSTEMDIR:"
  rep T:_uncompress "X_`Which $amigaosdisk:`Support/" "SYSTEMDIR:"
  Execute T:_uncompress
  Delete >NIL: T:_uncompress
Else
  ; uncompress .z files
  list "$amigaosdisk:" ALL FILES PAT=#?.Z LFORMAT="IF NOT EXISTS *"X_%f*"*NMakePath >NIL: *"X_%f*"*NENDIF*Ngunzip -d *"%f%n*" -c >*"X_%f%m*"" >T:_uncompress
  rep T:_uncompress "X_$amigaosdisk:" "SYSTEMDIR:"
  rep T:_uncompress "X_`Which $amigaosdisk:`" "SYSTEMDIR:"
  Execute T:_uncompress
  Delete >NIL: T:_uncompress
EndIf
