; Finalize
; --------
;
; Date: 2021-08-05
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to finalize installation of amiga os 3.2.

echo "Finalizing..."

; run uncompress fonts and locale
Execute "INSTALLDIR:S/Amiga-OS-3.2/Uncompress" "SYSTEMDIR:Fonts"
Execute "INSTALLDIR:S/Amiga-OS-3.2/Uncompress" "SYSTEMDIR:Locale"

; copy disk.info
IF EXISTS "SYSTEMDIR:Disk.info"
  Protect >NIL: "SYSTEMDIR:Disk.info" +prwed
ENDIF
IF NOT EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.2-GlowIcons"
  IF EXISTS "SYSTEMDIR:Temp/Update/Disk.info"
    Copy >NIL: "SYSTEMDIR:Temp/Update/Disk.info" "SYSTEMDIR:" CLONE
  ENDIF
ENDIF

; update dh1 disk icon, if dh1: device exists
Assign >NIL: EXISTS DH1:
IF $RC EQ 0 VAL
  IF EXISTS "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info"
    Copy >NIL: "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info" "DH1:Disk.info"
  ELSE
    Copy >NIL: "SYSTEMDIR:Disk.info" "DH1:Disk.info"
  ENDIF
ENDIF

; copy startup-sequence
IF EXISTS "SYSTEMDIR:S/Startup-Sequence"
  Protect >NIL: "SYSTEMDIR:S/Startup-Sequence" +prwed
  Delete >NIL: "SYSTEMDIR:S/Startup-Sequence"
ENDIF
Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Amiga-OS-32-Startup" "SYSTEMDIR:S/" CLONE
Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Startup-sequence" "SYSTEMDIR:S/" CLONE
Type "SYSTEMDIR:Temp/Update/Startup-HardDrive" >>"SYSTEMDIR:S/Startup-sequence"

; copy mmulib test cpu for patch startup sequence
Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/C/TestCPU" "SYSTEMDIR:C" CLONE

; copy release to versions
Copy >NIL: "SYSTEMDIR:Temp/Update/Release" "SYSTEMDIR:Prefs/Env-Archive/Versions" CLONE

; copy pal or ntsc monitor
IF EXISTS "INSTALLDIR:Prefs/Pal-Monitor"
  Copy >NIL: "SYSTEMDIR:Storage/Monitors/PAL" "SYSTEMDIR:Devs/Monitors" CLONE 
  Copy >NIL: "SYSTEMDIR:Storage/Monitors/PAL.info" "SYSTEMDIR:Devs/Monitors" CLONE  
ELSE
  Copy >NIL: "SYSTEMDIR:Storage/Monitors/NTSC" "SYSTEMDIR:Devs/Monitors" CLONE 
  Copy >NIL: "SYSTEMDIR:Storage/Monitors/NTSC.info" "SYSTEMDIR:Devs/Monitors" CLONE  
ENDIF

; run update wb files
Assign >NIL: NEWWB: SYSTEMDIR:
"SYSTEMDIR:C/UpdateWBFiles"

; clean up icons, if glow icons doesn't exist
IF NOT EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.2-GlowIcons"
  Execute "INSTALLDIR:S/Amiga-OS-3.2/Clean-Up"
ENDIF

; copy workbench prefs to use best icon quality and no icon border
IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/Workbench.prefs"
  Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/Workbench.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/Workbench.prefs" CLONE
ENDIF

; copy font prefs to use text only for workbench font
IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/Font.prefs"
  Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/Font.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/Font.prefs" CLONE
ENDIF

IF EXISTS "INSTALLDIR:Prefs/Pal-Monitor"
  IF EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.2-Screenmode"
    IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_PAL_640x512.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_PAL_640x512.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
      IF EXISTS "SYSTEMDIR:Prefs/Presets/Backdrops/default_pal.iff"
        Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/WBPattern_PAL_640x512.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/WBPattern.prefs" CLONE
      ENDIF
    ENDIF
  ELSE
    IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_PAL_640x256.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_PAL_640x256.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ENDIF
ELSE
  IF EXISTS "INSTALLDIR:Prefs/Amiga-OS-3.2-Screenmode"
    IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_NTSC_640x400.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_NTSC_640x400.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
      IF EXISTS "SYSTEMDIR:Prefs/Presets/Backdrops/default_ntsc.iff"
        Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/WBPattern_NTSC_640x400.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/WBPattern.prefs" CLONE
      ENDIF
    ENDIF
  ELSE
    IF EXISTS "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_NTSC_640x200.prefs"
      Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Prefs/ScreenMode_NTSC_640x200.prefs" TO "SYSTEMDIR:Prefs/Env-Archive/Sys/ScreenMode.prefs" CLONE
    ENDIF
  ENDIF
ENDIF
