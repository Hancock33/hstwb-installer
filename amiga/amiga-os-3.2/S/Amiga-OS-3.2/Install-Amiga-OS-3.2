; Install Amiga OS 3.2
; --------------------
;
; Date: 2022-01-03
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to automate installation of Amiga OS 3.2 from adf files or floppy disks.


; amiga os 3.2 installation message
cls
echo "*e[32m" NOLINE
echo "Amiga OS 3.2 Installation"
echo "*e[0m*e[1m" NOLINE
echo "---------------------------"
echo "*e[0m" NOLINE 
echo ""

; run amiga os 3.2 menu, if self install prefs doesn't exist
IF NOT EXISTS INSTALLDIR:Prefs/Self-Install
  Execute INSTALLDIR:S/Amiga-OS-3.2/Amiga-OS-3.2-Menu
ENDIF


; glow icons
LAB glowicons
set glowicons `RequestChoice "Install Glow Icons" "Amiga OS 3.2 comes with a colorful and*Nprofessionally designed icon set called Glow Icons.*N*NDo you want to install Amiga OS 3.2 Glow Icons?" "Help|Yes|No"`

; help option
IF "$glowicons" EQ 1 VAL
  IF EXISTS "INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt"
    Lister "INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt" >NIL:
  ELSE
    RequestChoice "Error" "Help file*N'INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt'*Ndoesn't exist!" "OK" >NIL:
  ENDIF
  SKIP BACK glowicons
ENDIF

; yes option
IF "$glowicons" EQ 2 VAL
  echo "" NOLINE >INSTALLDIR:Prefs/Amiga-OS-3.2-GlowIcons
  SKIP screenmode
ENDIF

SKIP install


; screensmode
; -----------
LAB screenmode

; screenmode
IF EXISTS "INSTALLDIR:Prefs/Pal-Monitor"
  set screenmode `RequestChoice "Update Screenmode" "Glow Icons looks best with interlaced screenmode.*N*NDo you want to update screenmode prefs with display mode*Nset to *"PAL 640 x 512, 256 colors*"?" "Help|Yes|No"`
ELSE
  set screenmode `RequestChoice "Update Screenmode" "Glow Icons looks best with interlaced screenmode.*N*NDo you want to update screenmode prefs with display mode*Nset to *"NTSC 640 x 400, 256 colors*"?" "Help|Yes|No"`
ENDIF

; help option
IF "$screenmode" EQ 1 VAL
  IF EXISTS "INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt"
    Lister "INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt" >NIL:
  ELSE
    RequestChoice "Error" "Help file*N'INSTALLDIR:Help/Amiga-OS-32-Glow-Icons.txt'*Ndoesn't exist!" "OK" >NIL:
  ENDIF
  SKIP BACK screenmode
ENDIF

; yes option
IF "$screenmode" EQ 2 VAL
  echo "" NOLINE >INSTALLDIR:Prefs/Amiga-OS-3.2-Screenmode
ENDIF


; install
; -------
LAB install

; create sys, icons directory, if it doesn't exist
IF NOT EXISTS "SYSTEMDIR:Icons/"
  MakePath >NIL: "SYSTEMDIR:Icons"
ENDIF

; copy install classic and glow icons scripts
Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Icons/#?" "SYSTEMDIR:Icons/"
Copy >NIL: "INSTALLDIR:Extras/Amiga-OS-3.2/Icons.info" "SYSTEMDIR:"

; run install amiga os 3.2 adf, if prefs exists. otherwise run install amiga os 3.2 disk
IF EXISTS "INSTALLDIR:Prefs/Install-Amiga-OS-32-ADF"
  ; run install amiga os 3.2 adf
  execute "INSTALLDIR:S/Amiga-OS-3.2/Install-Adf"
ELSE
  ; run install amiga os 3.2 disk
  execute "INSTALLDIR:S/Amiga-OS-3.2/Install-Disk"
ENDIF 

; set harddisk icon
set harddiskicon ""
IF EXISTS "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info"
  set harddiskicon "SYSTEMDIR:Prefs/Env-Archive/Sys/def_harddisk.info"
ELSE
  IF EXISTS "SYSTEMDIR:Disk.info"
    set harddiskicon "SYSTEMDIR:Disk.info"
  ENDIF
ENDIF

; update disk icon, if harddisk icon exists
IF NOT "$harddiskicon" EQ ""
  ; update dh1 disk icon, if dh1: device exists
  Assign >NIL: EXISTS DH1:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH1:Disk.info"
  ENDIF

  ; update dh2 disk icon, if dh2: device exists
  Assign >NIL: EXISTS DH2:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH2:Disk.info"
  ENDIF
ENDIF

; find set patch in startup sequence
Search SYSTEMDIR:S/Startup-Sequence "SetPatch" NONUM >T:_setpatch1
IF $RC EQ 0 VAL
  ; add boot startup to startup sequence after "SetPatch", if not present 
  Search >NIL: SYSTEMDIR:S/Startup-Sequence "Execute S:Boot-Startup" quiet
  IF NOT $RC EQ 0 VAL
    ; get found set patch text
    sed "1q;d" T:_setpatch1 >T:_setpatch2
    set setpatch `type T:_setpatch2`

    rep SYSTEMDIR:S/Startup-Sequence "$setpatch" "If Exists S:Boot-Startup*N  Execute S:Boot-Startup*NEndIf*N$setpatch"
  ENDIF
ELSE
  echo "*e[1mError: Unable to add boot startup,*N'SetPatch'*Nnot found in startup sequence.*NBoot startup can't be installed without this!*e[0m"
  ask "Press ENTER to continue"
ENDIF
Delete >NIL: T:_setpatch#?

; add mmulib startup to boot startup
Copy >>SYS:hstwb-installer.log "INSTALLDIR:Extras/Amiga-OS-3.2/Add-MMULib-Startup" "SYSTEMDIR:S/Boot-Startup"

; install mmulib startup
Copy >>SYS:hstwb-installer.log "INSTALLDIR:Extras/Amiga-OS-3.2/MMULib-Startup" "SYSTEMDIR:S"

; copy mmulib test cpu for patch startup sequence
Copy >>SYS:hstwb-installer.log "INSTALLDIR:Extras/Amiga-OS-3.2/C/TestCPU" "SYSTEMDIR:C" CLONE

; copy mmulib test cpu for mmulib startup
; install 680x0 libraries required for cpus and accelerator cards, if mmu library doesn't exist
IF NOT EXISTS "SYSTEMDIR:Libs/mmu.library"
  MakePath >NIL: "SYSTEMDIR:Temp"
  lha -m1 x "INSTALLDIR:Extras/Amiga-OS-3.2/MMULib.lha" "SYSTEMDIR:Temp/" >>SYS:hstwb-installer.log
  Copy >>SYS:hstwb-installer.log "SYSTEMDIR:Temp/MMULib/Libs/680?0.library" "SYSTEMDIR:Libs" CLONE
  Copy >>SYS:hstwb-installer.log "SYSTEMDIR:Temp/MMULib/Libs/mmu.library" "SYSTEMDIR:Libs" CLONE
  Delete >>SYS:hstwb-installer.log "SYSTEMDIR:Temp/MMULib" ALL
ENDIF

; create amiga os 3.2 prefs
echo "3.2" NOLINE >SYSTEMDIR:Prefs/AmigaOS
echo "" NOLINE >INSTALLDIR:Prefs/Amiga-OS-3.2

; get update install
set updateinstall "0"
If Exists "INSTALLDIR:Prefs/Install-Amiga-OS-321-ADF"
  set updateinstall "`type INSTALLDIR:Prefs/Install-Amiga-OS-321-ADF`"
EndIf

; install amiga os 3.2.1 update, if install update is 1
IF "$updateinstall" EQ 1 VAL
  ; run install amiga os 3.2.1 adf
  execute "INSTALLDIR:S/Amiga-OS-3.2/Install-Adf-3.2.1"

  ; create amiga os 3.2.1 prefs
  echo "3.2.1" NOLINE >SYSTEMDIR:Prefs/AmigaOS
  echo "" NOLINE >INSTALLDIR:Prefs/Amiga-OS-3.2.1
ENDIF 

echo ""
echo "Amiga OS 3.2 installation is complete."
echo ""
ask "Press ENTER to continue"
